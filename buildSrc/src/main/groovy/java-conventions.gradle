plugins {
    id "java"
    id "jacoco"
    id "pmd"
    id "checkstyle"
    id "com.github.spotbugs"
    id "io.freefair.lombok"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.release = 8
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                minimum = jacocoMinRatio as BigDecimal
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

jar.dependsOn check

checkstyle {
    configDirectory = file("$rootDir/gradle/config/checkstyle")
    toolVersion = "9.3"
}

pmd {
    consoleOutput = true
    ruleSets = ["ruleset.xml"]
}

tasks.withType(Pmd).configureEach {
    pmdClasspath += files("$rootDir/gradle/config/pmd")
}

spotbugs {
    omitVisitors = ["ConstructorThrow", "DoInsideDoPrivileged", "FindReturnRef"]
}

repositories {
    mavenCentral()
}

configurations {
    internalPlatform {
        visible = false
    }
    [compileClasspath, runtimeClasspath, testCompileClasspath, testRuntimeClasspath].each {
        it.extendsFrom internalPlatform
    }
}

dependencies {
    internalPlatform platform(project(":jdoc-platform"))

    testImplementation "org.assertj:assertj-core"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-junit-jupiter"
}